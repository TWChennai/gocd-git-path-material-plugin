plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.33.0'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

group 'com.thoughtworks.go'

sourceCompatibility = 1.9
targetCompatibility = 1.9

gocdPlugin {
    id = 'git-path'
    pluginVersion = '2.1.0'
    goCdVersion = '18.10.0'
    name = 'Git Path Material Plugin'
    description = 'Plugin that polls a Git repository and triggers pipelines based on sub-directory path matches'
    vendorName = 'ThoughtWorks, Inc / GoCD contributors'
    vendorUrl = 'https://github.com/' + (System.getenv('GITHUB_USER') ?: 'TWChennai') + '/gocd-git-path-material-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'TWChennai'
        repo = 'gocd-git-path-material-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.jar]
}

allprojects {
    version = gocdPlugin.fullVersion(project)
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

check.dependsOn jacocoTestReport

ext {
    powermockVersion = '2.0.9'
}

dependencies {
    implementation "cd.go.plugin:go-plugin-api:${gocdPlugin.goCdVersion}"

    implementation 'in.ashwanthkumar:git-cmd:2.0'
    implementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    implementation 'org.apache.commons:commons-lang3:3.11'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'org.mockito:mockito-core:3.5.15'
    testImplementation "org.powermock:powermock-core:${powermockVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

task startGoCd(dependsOn: build, type: Exec) {
    dependsOn build
    environment("PLUGIN_VERSION", "$version")
    commandLine "docker-compose", "up", "-d"
}

task restartGoCd(dependsOn: build, type: Exec) {
    dependsOn build
    environment("PLUGIN_VERSION", "$version")
    commandLine "docker-compose", "restart"
}

task stopGoCd(type: Exec) {
    environment("PLUGIN_VERSION", "$version")
    commandLine "docker-compose", "stop"
}