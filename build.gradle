plugins {
    id 'java'
    id 'jacoco'
}

apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 86400)}"

group 'com.thoughtworks.go'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

gocdPlugin {
    id = 'git-path'
    pluginVersion = '3.0.0'
    goCdVersion = '22.1.0'
    name = 'Git Path Material Plugin'
    description = 'Plugin that polls a Git repository and triggers pipelines based on sub-directory path matches'
    vendorName = 'Thoughtworks, Inc / GoCD contributors'
    vendorUrl = 'https://github.com/' + (System.getenv('GITHUB_USER') ?: 'TWChennai') + '/gocd-git-path-material-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'TWChennai'
        repo = 'gocd-git-path-material-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    prereleaseDryrun = !"Yes".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.jar]
}

allprojects {
    version = gocdPlugin.fullVersion(project)
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    gocdPluginVersion = '25.3.0'
}

dependencies {
    compileOnly "cd.go.plugin:go-plugin-api:${gocdPluginVersion}"

    implementation 'joda-time:joda-time:2.14.0'
    implementation 'org.apache.commons:commons-exec:1.5.0'
    implementation 'commons-io:commons-io:2.21.0'

    implementation platform('com.fasterxml.jackson:jackson-bom:2.20.1')
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'org.apache.commons:commons-lang3:3.20.0'

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testCompileOnly "cd.go.plugin:go-plugin-api:${gocdPluginVersion}"
    testImplementation 'org.mockito:mockito-junit-jupiter:5.20.0'
    testImplementation 'org.assertj:assertj-core:3.27.6'
    testImplementation 'uk.org.webcompere:system-stubs-jupiter:2.1.8'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jar {
    from(configurations.runtimeClasspath) {
        into "lib/"
    }
}

tasks.register("startGoCd", Exec) {
    dependsOn build
    environment("PLUGIN_VERSION", "$version")
    environment("GOCD_VERSION", "$gocdPluginVersion")
    commandLine "docker-compose", "up", "-d"
}

tasks.register("restartGoCd", Exec) {
    dependsOn build
    environment("PLUGIN_VERSION", "$version")
    environment("GOCD_VERSION", "$gocdPluginVersion")
    commandLine "docker-compose", "restart"
}

tasks.register("stopGoCd", Exec) {
    environment("PLUGIN_VERSION", "$version")
    environment("GOCD_VERSION", "$gocdPluginVersion")
    commandLine "docker-compose", "stop"
}
